# Maintainer: PitBall

pkgbase=mesa-cl
pkgname=('ati-dri-cl' 'intel-dri-cl' 'nouveau-dri-cl' 'svga-dri-cl' 'mesa-libcl' 'mesa-cl' 'mesa-libgl-cl')
pkgver=10.2.1
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2' 'libxml2' 'libxvmc' 'glproto' 'libdrm' 'dri2proto' 'dri3proto' 'presentproto' 'libxxf86vm'
             'libxdamage' 'libxshmfence' 'libxxf86vm' 'libvdpau' 'wayland' 'elfutils' 'llvm' 'systemd' 'libclc-svn')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(#http://cgit.freedesktop.org/mesa/mesa/snapshot/mesa-$pkgver.tar.gz
        ftp://ftp.freedesktop.org/pub/mesa/${pkgver/.0//}/MesaLib-${pkgver}.tar.bz2
        LICENSE .AURINFO)
md5sums=('093f9b5d077e5f6061dcd7b01b7aa51a'
         '5c65a0fe315dd347e09b1f2826a1df5a'
         SKIP)

build() {
    cd ${srcdir}/?esa-*
    autoreconf -vfi # our automake is far too new for their build system :)

    ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-egl-platforms=x11,drm,wayland \
    --with-llvm-shared-libs \
    --enable-r600-llvm-compiler \
    --enable-gallium-llvm \
    --enable-gallium-egl \
    --enable-gallium-gbm \
    --enable-gallium-osmesa \
    --enable-egl \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-dri3 \
    --enable-glx \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-opencl \
    --enable-xa \
    --enable-vdpau


    make
    # fake installation
	install -d $srcdir/fakeinstall
	make DESTDIR=${srcdir}/fakeinstall install
}

package_ati-dri-cl() {
  pkgdesc="Mesa drivers for AMD/ATI Radeon"
  depends=("mesa-libgl-cl=${pkgver}")
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  provides=("ati-dri=${pkgver}")
  replaces=('ati-dri')
  conflicts=('ati-dri' 'xf86-video-ati<7.2.0-1')

  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_r*.* ${pkgdir}/usr/lib/vdpau/

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/r*_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/
  
  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_r* ${pkgdir}/usr/lib/gallium-pipe/
  
  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
}

package_intel-dri-cl() {
  pkgdesc="Mesa drivers for Intel"
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  depends=("mesa-libgl-cl=${pkgver}")
  provides=("intel-dri=${pkgver}")
  replaces=('intel-dri')
  conflicts=('intel-dri')

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"
}

package_nouveau-dri-cl() {
  pkgdesc="Mesa drivers for Nouveau"
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  depends=("mesa-libgl-cl=${pkgver}")
  provides=("nouveau-dri=${pkgver}")
  replaces=('novueau-dri')
  conflicts=('novueau-dri')

  # vdpau driver is problematic, so remove it (FS#36004)
  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_nouveau.* ${pkgdir}/usr/lib/vdpau/
  rm -rf ${pkgdir}/usr/lib/vdpau/

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib/xorg/modules/dri/
  
  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib/gallium-pipe/
  
  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/nouveau-dri/"
}

package_svga-dri-cl() {
  pkgdesc="Gallium3D VMware guest GL driver"
  depends=('libdrm' 'expat' 'llvm-libs')
  provides=("svga-dri=${pkgver}")
  replaces=('svga-dri')
  conflicts=('svga-dri')

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/vmwgfx_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_vmwgfx* ${pkgdir}/usr/lib/gallium-pipe/
  
  install -m755 -d "${pkgdir}/usr/share/licenses/svga-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/svga-dri/"
}

package_mesa-libcl() {
  pkgdesc="Mesa CL library"
  depends=("mesa-cl=${pkgver}" 'libffi' 'opencl-headers')
  provides=("libcl=${pkgver}")
  replaces=('libcl')
  conflicts=('libcl')
  install -m755 -d ${pkgdir}/usr/lib
  mv -v ${srcdir}/fakeinstall/usr/lib/libOpenCL*  ${pkgdir}/usr/lib/
  install -m755 -d ${pkgdir}/usr/share/licenses/mesa-libcl
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libcl/"
}

package_mesa-cl() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'wayland' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'udev' 'llvm-libs' 'elfutils')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=("mesa=${pkgver}" 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  cp -vr ${srcdir}/fakeinstall/* ${pkgdir}
  find ${pkgdir} -type d -exec chmod 755 {} \;
  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0 ${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  mv -v ${pkgdir}/usr/lib/libEGL.so.1.0.0 ${pkgdir}/usr/lib/mesa-libEGL.so.1.0.0
  mv -v ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/mesa-libGLESv1_CM.so.1.1.0
  mv -v ${pkgdir}/usr/lib/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/mesa-libGLESv2.so.2.0.0


  rm ${pkgdir}/usr/lib/lib{,E}GL.so{,.1}
  rm ${pkgdir}/usr/lib/libGLES*.so{,.1,.2}
  #provided by opencl-headers
  rm -rf ${pkgdir}/usr/include/CL

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_mesa-libgl-cl() {
  pkgdesc="Mesa 3-D graphics library"
  depends=("mesa-cl=${pkgver}")
  provides=("mesa-libgl=${pkgver}" "libgl=${pkgver}")
  replaces=('libgl')
  conflicts=('mesa-libgl')
  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0         ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0         ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0         ${pkgdir}/usr/lib/libGL.so.1.2.0
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib/libEGL.so
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib/libEGL.so.1
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib/libEGL.so.1.0.0
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib/libGLESv1_CM.so
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib/libGLESv1_CM.so.1
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib/libGLESv2.so
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib/libGLESv2.so.2
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib/libGLESv2.so.2.0.0


  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"
}
